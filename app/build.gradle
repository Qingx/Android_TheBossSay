plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'com.huawei.agconnect'

}

def rootPro = rootProject.ext
def rootAndroid = rootPro.android

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "net.cd1369.tbs.android"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode rootAndroid.versionCode
        versionName rootAndroid.versionName
        flavorDimensions "1.3"

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86_64"
        }
    }

    signingConfigs {
        release {
            v1SigningEnabled true
            v2SigningEnabled true

            keyAlias "tbs_release"
            keyPassword "${pwd}"
            storeFile file("../config/tbs")
            storePassword "${pwd}"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            zipAlignEnabled true
            shrinkResources false
            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "app_name_env", "Boss说"

            buildConfigField("String", "FILE_URL", "\"http://192.144.169.58:9999/\"")
            buildConfigField("String", "DOWN_URL", "\"http://file.tianjiemedia.com/\"")
            buildConfigField("String", "BASE_URL", "\"http://api.tianjiemedia.com/\"") //阿里云
        }

        debug {
            minifyEnabled false
            debuggable true
            zipAlignEnabled true
            signingConfig signingConfigs.release

            resValue "string", "app_name_env", "Boss说"

            buildConfigField("String", "FILE_URL", "\"http://192.144.169.58:9999/\"")
            buildConfigField("String", "DOWN_URL", "\"http://file.tianjiemedia.com/\"")
            buildConfigField("String", "BASE_URL", "\"http://192.168.1.142:8087/\"") //阿里云
//            buildConfigField("String", "BASE_URL", "\"http://182.92.221.30:8086/\"") //阿里云
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    productFlavors {
        DEV {
            versionCode rootAndroid.testVersionCode
            versionName "${rootAndroid.testVersionName}.${releaseTime()}"
        }

        UAT {} // 普通打包
        MI {}  // 小米
        YYB {} // 应用宝
        OPPO {} // oppo渠道
        VIVO {} // vivo渠道
        C360 {} // 360渠道
        BaiDu {} // 百度渠道
        HuaWei {} // 华为应用市场渠道
        SouGou {} // 搜狗应用市场
        MeiZu {} // 魅族应用市场
    }

    //如果需要在不同渠道统一配置，使用productFlavors.all字段
    productFlavors.all { flavor ->
        def name = flavor.name

        flavor.buildConfigField("String", "ENV", "\"$name\"")
        flavor.manifestPlaceholders = [
                APP_Channel   : name,
                TD_CHANNEL_ID : name,
                JPUSH_PKGNAME : "net.cd1369.tbs.android",
                JPUSH_APPKEY  : "f98cfa8459c6510944ebbefd", //JPush 上注册的包名对应的 Appkey.
                JPUSH_CHANNEL : "developer-default", //暂时填写默认值即可.

                XIAOMI_APPKEY : "MI-5252001773328", // 小米平台注册的appkey,注意不要将前缀去掉 MI-appkey
                XIAOMI_APPID  : "MI-2882303761520017328", // 小米平台注册的appid

                VIVO_APPKEY   : "79a0cd7a4bbd0ea9a98caf938a690bcf", // VIVO平台注册的appkey
                VIVO_APPID    : "105499023", // VIVO平台注册的appid

                OPPO_APPKEY   : "OP-29f55d122cf1499e8261fce388d56892", // OPPO平台注册的appkey
                OPPO_APPID    : "OP-30592681", // OPPO平台注册的appid
                OPPO_APPSECRET: "OP-7bb887f022184320bf0e1c0c3cad6c2b",//OPPO平台注册的appsecret

                MEIZU_APPKEY  : "MZ-1d17422e92fd446f94c177512b68df13", // 魅族平台注册的appkey
                MEIZU_APPID   : "MZ-144452", // 魅族平台注册的appid
        ]
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

/**
 * 按发布时间生成版本号
 * @return
 */
def releaseTime() {
    return new Date().format("dd-HH-mm", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':wllib')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation files('libs/SaaS_AppAnalytics_Android_SDK_V4.0.65.jar')
    implementation 'androidx.palette:palette-ktx:1.0.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.github.zerobranch:SwipeLayout:1.3.1'
    implementation 'com.github.mcxtzhang:SwipeDelMenuLayout:V1.3.0'//recyclerView左滑删除
    implementation 'com.jyn.verificationcodeview:verificationcodeview:1.0.1'
    implementation 'com.irozon.sneaker:sneaker:2.0.0'
    implementation 'com.google.zxing:core:3.3.0'

    implementation 'cn.jiguang.sdk:jpush:4.3.0'
    implementation 'cn.jiguang.sdk:jverification:2.7.1'
    implementation 'cn.jiguang.sdk:jcore:2.9.0'

    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:5.4.0'

    def advanceVersion = "3.5.1"
    //必须，Advance聚合SDK 引入。如果gradle引入异常，请参考根目录gradle进行配置，如果依然无法引入，请联系我们使用本地aar引入。
    implementation "io.github.flayone:advance-core:${advanceVersion}_2"
    //必须，mercury SDK 引入 包含了广告aar以及渠道处理逻辑
    implementation "io.github.flayone:advance-mry:${advanceVersion}_3451"
    //穿山甲、广点通、百度、快手SDK 引入，包含了广告aar以及渠道处理逻辑(如果APP本地引入了平台SDK的aar，不希望使用带aar引入的advance渠道库，可以在最后面加上 _core 标识，这样就不会产生aar引入冲突了，但是请注意，非advance推荐版本可能会出现广告加载异常，一定要充分测试)
    implementation "io.github.flayone:advance-gdt:${advanceVersion}_4360"
    implementation "io.github.flayone:advance-csj:${advanceVersion}_3613"
    implementation "io.github.flayone:advance-bd:${advanceVersion}_596"
    implementation "io.github.flayone:advance-ks:${advanceVersion}_339"

    implementation "com.github.gzu-liyujiang:Android_CN_OAID:4.2.1"
    implementation "io.coil-kt:coil:1.0.0"
    implementation 'cn.tseeey:justtextview:0.1.0'
    implementation 'com.youth.banner:banner:2.1.0'

    //华为推送
    implementation 'com.huawei.agconnect:agconnect-core:1.5.2.300'
    implementation 'cn.jiguang.sdk.plugin:huawei:4.1.5'
    implementation 'com.huawei.hms:push:5.3.0.301'

    implementation 'cn.jiguang.sdk.plugin:xiaomi:4.3.0'//小米推送
    implementation 'cn.jiguang.sdk.plugin:vivo:4.3.0' //VIVO推送
    implementation 'cn.jiguang.sdk.plugin:oppo:4.3.0' //OPPO推送
    implementation 'cn.jiguang.sdk.plugin:meizu:4.3.0' //魅族推送

    implementation 'com.tencent.map:sdk-utilities:1.0.6'

    // 在dependencies中添加，版本号以赋能平台生成的maven命令为准
    implementation ('com.pangle.cn:pangrowth-sdk:1.6.1.0'){
        exclude group: 'com.pangle.cn', module: 'pangrowth-dpsdk-live'
        exclude group: 'com.pangle.cn', module: 'pangrowth-novel-sdk'
        exclude group: 'com.pangle.cn', module: 'pangrowth-game-sdk'
        exclude group: 'com.pangle.cn', module: 'pangrowth-luckycat-sdk'
    }
}